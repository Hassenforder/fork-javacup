
/*================================================================*/ 
/* 
  JavaCup Specification for the JavaCup Specification Language
  by Scott Hudson, GVU Center, Georgia Tech, August 1995
  and Frank Flannery, Department of Computer Science, Princeton Univ,
  July 1996
  Bug Fixes: C. Scott Ananian, Dept of Electrical Engineering, Princeton
  University, October 1996. [later Massachusetts Institute of Technology]


  This JavaCup specification is used to implement JavaCup itself.
  It specifies the parser for the JavaCup specification language.
  (It also serves as a reasonable example of what a typical JavaCup
  spec looks like).

  The specification has the following parts:
    Package and import declarations
      These serve the same purpose as in a normal Java source file
      (and will appear in the generated code for the parser). In this 
      case we are part of the javacup package and we import both the
      javacup runtime system and Hashtable from the standard Java
      utilities package.

    Action code
      This section provides code that is included with the class encapsulating
      the various pieces of user code embedded in the grammar (i.e., the
      semantic actions).  This provides a series of helper routines and
      data structures that the semantic actions use.

    Parser code    
      This section provides code included in the parser class itself.  In
      this case we override the default error reporting routines.

    Init with and scan with 
      These sections provide small bits of code that initialize, then 
      indicate how to invoke the scanner.

    Symbols and grammar
      These sections declare all the terminal and non terminal symbols 
      and the types of objects that they will be represented by at runtime,
      then indicate the start symbol of the grammar (), and finally provide
      the grammar itself (with embedded actions).

    Operation of the parser
      The parser acts primarily by accumulating data structures representing
      various parts of the specification.  Various small parts (e.g., single
      code strings) are stored as static variables of the emit class and
      in a few cases as variables declared in the action code section.  
      Terminals, non terminals, and productions, are maintained as collection
      accessible via static methods of those classes.  In addition, two 
      symbol tables are kept:  
	symbols   maintains the name to object mapping for all symbols

      Several intermediate working structures are also declared in the action
      code section.  These include: rhs_parts, rhs_pos, and lhs_nt which
      build up parts of the current production while it is being parsed.
	
  Author(s)
    Scott Hudson, GVU Center, Georgia Tech.
    Frank Flannery, Department of Computer Science, Princeton Univ.
    C. Scott Ananian, Department of Electrical Engineering, Princeton Univ.

  Revisions
    v0.9a   First released version                     [SEH] 8/29/95
    v0.9b   Updated for beta language (throws clauses) [SEH] 11/25/95
    v0.10a  Made many improvements/changes. now offers:
              return value
              left/right positions and propagations
              cleaner label references
              precedence and associativity for terminals
              contextual precedence for productions
              [FF] 7/3/96
    v0.10b  Fixed %prec directive so it works like it's supposed to.
              [CSA] 10/10/96
    v0.10g   Added support for array types on symbols.
              [CSA] 03/23/98
    v0.10i  Broaden set of IDs allowed in multipart_id and label_id so
            that only java reserved words (and not CUP reserved words like
            'parser' and 'start') are prohibited.  Allow reordering of
	    action code, parser code, init code, and scan with sections,
	    and made closing semicolon optional for these sections.
	    Added 'nonterminal' as a terminal symbol, finally fixing a
	    spelling mistake that's been around since the beginning.
	    For backwards compatibility, you can still misspell the
	    word if you like.
    v0.11a  Added support for generic types on symbols.
    v0.12a  Clean up, added options, added parser name.
    v0.14   Added support for *, +, ? after symbols.
*/
/*================================================================*/ 

package com.github.jhoenicke.javacup;
import com.github.jhoenicke.javacup.runtime.*;
import java.util.HashMap;
import java.util.ArrayList;
import java.util.Arrays;
/*----------------------------------------------------------------*/ 

option java15, compact_red, interface, newpositions;

action code {:
  Grammar grammar = new Grammar();

  /** table of declared symbols -- contains production parts indexed by name */
  private HashMap<String, GrammarSymbol> symbols = new HashMap<String, GrammarSymbol>();

  /** left hand side non terminal of the current production */
  private NonTerminal lhs_nt;

  {
    /* declare "error" and "EOF" as a symbols */
    symbols.put("error", Terminal.error);
    symbols.put("EOF", Terminal.EOF);
  }  

  /** true, if declaring non-terminals. */
  boolean _cur_is_nonterm;
  /** Current symbol type */
  String _cur_symbol_type;

  /** Current precedence number */
  int _cur_prec = 0;

  /** Current precedence side */
  int _cur_side = Assoc.NOPREC;

  /** update the precedences we are declaring */
  protected void update_precedence(int p)
    {
      _cur_side = p;
      _cur_prec++;
    }
  
  private Terminal getTerminal(com.github.jhoenicke.javacup.runtime.Symbol location, String id)
    {
      GrammarSymbol sym = symbols.get(id);

      /* if it wasn't declared of the right type, emit a message */
      if (!(sym instanceof Terminal))
	{
	    ErrorManager.getManager().emit_warning("Terminal \"" + id + 
	    "\" has not been declared", location);
	  return null;
	}
      return (Terminal)sym;
    }
  
  private NonTerminal getNonterminal(com.github.jhoenicke.javacup.runtime.Symbol location, String id)
    {
      GrammarSymbol sym = symbols.get(id);

      /* if it wasn't declared of the right type, emit a message */
      if (!(sym instanceof NonTerminal))
	{
	    ErrorManager.getManager().emit_warning("Non-terminal \"" + id + 
	    "\" has not been declared", location);
	  return null;
	}
      return (NonTerminal)sym;
    }
:};

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

parser code {:
  Options options;
  
 /* override error routines */
  public void report_fatal_error(
    String   message,
    Object   info)
    {
      done_parsing();
      if (info instanceof com.github.jhoenicke.javacup.runtime.Symbol) ErrorManager.getManager().emit_fatal(message+ "\nCan't recover from previous error(s), giving up.",(com.github.jhoenicke.javacup.runtime.Symbol)info);
      else ErrorManager.getManager().emit_fatal(message + "\nCan't recover from previous error(s), giving up.",cur_token);
      throw new RuntimeException("jcup fatal error parsing grammar");
    }

    public void report_error(String message, Object info)
    {
      if (info instanceof com.github.jhoenicke.javacup.runtime.Symbol)
         ErrorManager.getManager().emit_error(message,(com.github.jhoenicke.javacup.runtime.Symbol)info);
      else
         ErrorManager.getManager().emit_error(message,cur_token);
    }
:};

/*----------------------------------------------------------------*/ 

terminal
  SEMI, COMMA, STAR, DOT, COLON, COLON_COLON_EQUALS, BAR, 
  PERCENT_PREC, LBRACK, RBRACK, LPARENT, RPARENT, GT, LT, QUESTION, EQUALS, PLUS;

terminal String
  PACKAGE, IMPORT, CODE, ACTION, PARSER, TERMINAL, NON, NONTERMINAL, INIT, 
  SCAN, WITH, START, PRECEDENCE, LEFT, RIGHT, NONASSOC, SUPER, EXTENDS, 
  AFTER, REDUCE, OPTION, EXPECT;

terminal String  ID, CODE_STRING;

non terminal
  spec,
  package_spec, parser_spec,
  option_spec, option_list, option_, action_code_part,
  parser_code_part, start_spec, expect_spec,
  import_spec, init_code, scan_code, after_reduce_code, symbol,
  terminal_non_terminal, decl_symbol_list, new_symbol_id,
  preced, assoc, precterminal_list, precterminal_id,
  production, rhs_list, rhs;

non terminal Grammar grammar;

non terminal String  symbol_id, label_id, robust_id;
non terminal StringBuilder multipart_id, import_id, type_id,
			   typearglist, typeargument, wildcard;
non terminal GrammarSymbol wild_symbol_id;

non terminal ProductionPart prod_part;
non terminal GrammarSymbol prod_precedence;

/*----------------------------------------------------------------*/

start with grammar;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

grammar	::=	spec*
	        {: RESULT = grammar; :}
		;

spec ::=	package_spec
		|	import_spec
	    |	option_spec
	    |	parser_spec
	    |	action_code_part
	    |	parser_code_part
	    |	init_code
	    |	scan_code
	    |	after_reduce_code
		|	symbol
		|	preced
		|	expect_spec
		|	start_spec
		|	production
		|	error SEMI
		;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

package_spec ::=
	PACKAGE	multipart_id:id SEMI
	{:
	  /* save the package name */
	  parser.options.setOption("package", id.toString());
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

import_spec ::=
	IMPORT import_id:id SEMI
	{:
	  /* save this import on the imports list */
	  parser.options.import_list.add(id.toString());
	:}
	;


/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

parser_spec ::=
	  PARSER multipart_id:name SEMI
         {: parser.options.setOption("parser", name.toString()); :}
	| PARSER multipart_id:name LT typearglist:types GT SEMI
         {: parser.options.setOption("parser", name.toString());
	    parser.options.setOption("typearg", types.toString()); :}
       	;


option_spec ::= OPTION option_list SEMI;
option_list ::= option_list COMMA option_ | option_;
option_ ::= robust_id:opt {: parser.options.setOption(opt); :}
	| robust_id:opt EQUALS robust_id:val
          {: parser.options.setOption(opt, val); :};
/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

action_code_part ::=
	ACTION CODE CODE_STRING:user_code SEMI?
	{:
	  if (parser.options.action_code!=null)
	    ErrorManager.getManager().emit_warning("Redundant action code (skipping)");
	  else /* save the user included code string */
	    parser.options.action_code = user_code;
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

parser_code_part ::=
	PARSER CODE CODE_STRING:user_code SEMI?
	{:
	  if (parser.options.parser_code!=null)
	    ErrorManager.getManager().emit_warning("Redundant parser code (skipping)");
	  else /* save the user included code string */
	    parser.options.parser_code = user_code;
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

init_code ::=
	INIT WITH CODE_STRING:user_code SEMI?
	{:
	  if (parser.options.init_code!=null)
	    ErrorManager.getManager().emit_warning("Redundant init code (skipping)");
	  else /* save the user code */
	    parser.options.init_code = user_code;
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

scan_code ::=
	SCAN WITH CODE_STRING:user_code SEMI?
	{:
	  if (parser.options.scan_code!=null)
	    ErrorManager.getManager().emit_warning("Redundant scan code (skipping)");
	  else /* save the user code */
	    parser.options.scan_code = user_code;
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

after_reduce_code ::=
	AFTER REDUCE CODE_STRING:user_code SEMI?
	{:
	  if (parser.options.after_reduce_code!=null)
	    ErrorManager.getManager().emit_warning("Redundant after reduce code (skipping)");
	  else /* save the user code */
	    parser.options.after_reduce_code = user_code;
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

symbol ::= 
	terminal_non_terminal
	type_id:id  {: _cur_symbol_type = id.toString(); :}
	decl_symbol_list SEMI {: _cur_symbol_type = null; :}
	|
	terminal_non_terminal
	decl_symbol_list SEMI {: _cur_symbol_type = null; :}
	;
	
/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

terminal_non_terminal ::=
	  TERMINAL     {: _cur_is_nonterm = false; :} 
        | NON TERMINAL {: _cur_is_nonterm = true; :}
        | NONTERMINAL  {: _cur_is_nonterm = true; :};

decl_symbol_list ::= decl_symbol_list COMMA new_symbol_id 
	| new_symbol_id;

new_symbol_id ::= 
	symbol_id:sym_id 
	{: 
	  /* see if this terminal has been declared before */
	  if (symbols.get(sym_id) != null)
	    {
	      /* issue a message */
	      ErrorManager.getManager().emit_error("Symbol \"" + sym_id + 
			   "\" has already been declared", sym_id$);
	    }
	  else
	    {
	      /* build the symbol and put it in the symbol table */
	      GrammarSymbol sym;
	      if (_cur_is_nonterm) 
	      	sym = grammar.addNonterminal(sym_id, _cur_symbol_type);
	      else
	      	sym = grammar.addTerminal(sym_id, _cur_symbol_type);
	      symbols.put(sym_id, sym);
	    }
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

preced ::= PRECEDENCE assoc precterminal_list SEMI;

assoc ::= LEFT     {: update_precedence(Assoc.LEFT); :}
        | RIGHT    {: update_precedence(Assoc.RIGHT); :}
        | NONASSOC {: update_precedence(Assoc.NONASSOC); :}
        ;
      
/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

precterminal_list ::= precterminal_list COMMA precterminal_id
	|
	precterminal_id
	;

precterminal_id ::= symbol_id:term
	{:	
	  getTerminal(term$, term).setPrecedence(_cur_side, _cur_prec);
	:};
	  
/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

expect_spec ::= 
	EXPECT robust_id:val SEMI 
	{: parser.options.setOption("expect", val); :}
	;

start_spec ::= 
	START WITH symbol_id:start_name SEMI 
	{: NonTerminal nt = getNonterminal(start_name$, start_name);
	   if (nt != null) 
	     grammar.setStartSymbol(nt); 
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

production ::=
	symbol_id:lhs_id
	{:
	  /* lookup the lhs nt */
	  lhs_nt = getNonterminal(lhs_id$, lhs_id);
	:}
	COLON_COLON_EQUALS
	rhs_list
	SEMI
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

rhs_list ::= rhs_list BAR rhs | rhs;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

prod_precedence ::= PERCENT_PREC symbol_id:term
	{: RESULT = getTerminal(term$, term); :}
	| /* empty */ {: RESULT = null; :};
	      

rhs ::= 
	prod_part*:rhs prod_precedence:precsym 
	{:
	  if (lhs_nt != null) 
	    {
	      /* build the production */
	      ArrayList<ProductionPart> rhs_list = 
	      	new ArrayList<ProductionPart>(rhs.length);
	      rhs_list.addAll(Arrays.asList(rhs));
	      grammar.buildProduction(lhs_nt, rhs_list, (Terminal) precsym);
	    }
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

prod_part ::=
	wild_symbol_id:symb label_id?:labid
	{: 
	  /* add a labeled production part */
	  RESULT = new SymbolPart(symb, labid);
	:}
	|
	CODE_STRING:code_str
	{: 
	  /* add a new production part */
	  RESULT = new ActionPart(code_str);
	:}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */

wild_symbol_id ::=
    wild_symbol_id:s STAR 
    {: RESULT = grammar.getStarSymbol(s); :} 
    |
    wild_symbol_id:s PLUS 
    {: RESULT = grammar.getPlusSymbol(s); :} 
    |
    wild_symbol_id:s QUESTION
    {: RESULT = grammar.getOptSymbol(s); :} 
    |
    symbol_id : symid
    {: /* try to look up the id */
	   GrammarSymbol symb = symbols.get(symid);

	   /* if that fails, symbol is undeclared */
	   if (symb == null)
	     {
	         ErrorManager.getManager().emit_error("Symbol \"" + symid + 
			        "\" has not been declared", symid$);
	       RESULT = null;
	     }
	   else
	     {
           RESULT = symb; 
         }
    :}
    |
    LPARENT prod_part+:prod RPARENT
    {:
	      ArrayList<ProductionPart> rhs_list = 
	      	new ArrayList<ProductionPart>(prod.length);
	      rhs_list.addAll(Arrays.asList(prod));
    	RESULT = grammar.getParenthesisSymbol(rhs_list);
	:}
    ;

label_id ::=
	COLON robust_id:labid
	{: RESULT = labid; :}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

multipart_id ::=
	multipart_id:id DOT robust_id:another_id
	{: id.append('.').append(another_id); RESULT=id; :}
        |
	robust_id:an_id
	{: RESULT = new StringBuilder(an_id); :}
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

import_id ::= 
	multipart_id:id DOT STAR
	{: id.append(".*"); RESULT = id; :}
	|
	multipart_id
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

type_id ::= multipart_id
	| type_id:id LBRACK RBRACK
	{: id.append("[]"); RESULT = id; :}
	|multipart_id:id 
        LT typearglist:types GT
         {: id.append('<').append(types).append('>');
            RESULT=id; :}
       	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

typearglist ::= 
            typeargument
            | typearglist:list COMMA typeargument:arg
            {: RESULT = list.append(",").append(arg); :}
            ;

typeargument ::=
              type_id
              | wildcard
              ;

wildcard ::=
         QUESTION
         {: RESULT = new StringBuilder("?"); :}
         | wildcard:w EXTENDS type_id:id
         {: RESULT = w.append(" extends ").append(id); :}
         | wildcard:w SUPER type_id:id
         {: RESULT = w.append(" super ").append(id); :}
         ;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

symbol_id ::=
	ID
	| OPTION
	| SUPER
	| EXTENDS
	| CODE
	| ACTION
	| PARSER
	| INIT
	| SCAN
	| WITH
	| LEFT
	| RIGHT
	| NONASSOC
	| AFTER
	| REDUCE
	;

/*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . */ 

robust_id ::= /* all ids that aren't reserved words in Java */
	ID
	/* package is reserved. */
	/* import is reserved. */
	| OPTION
	| CODE
	| ACTION
	| PARSER
	| TERMINAL
	| NON
	| NONTERMINAL
	| INIT
	| SCAN
	| WITH
	| START
	| EXPECT
	| PRECEDENCE
	| LEFT
	| RIGHT
	| NONASSOC
	| AFTER
	| REDUCE
	| error:e
	{:
		ErrorManager.getManager().emit_error("Illegal use of reserved word", e$);
		RESULT="ILLEGAL";
	:}
	;

/*----------------------------------------------------------------*/ 

